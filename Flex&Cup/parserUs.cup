package com.triviapp.analizadores.sintactico;

import java.util.ArrayList;
import java.util.List;
import com.triviapp.modelo.Componente;
import com.triviapp.modelo.Formulario;
import com.triviapp.modelo.Usuario;
import com.triviapp.modelo.DatoRecopilado;
import com.triviapp.modelo.errores.ErrorAnalisis;
import java_cup.runtime.Symbol;

parser code {:
    
    private List<ErrorAnalisis> errores = new ArrayList();
    private Usuario user = new Usuario();
    private Formulario form = new Formulario();
    private Componente comp = new Componente();
    private List<DatoRecopilado> datos = new ArrayList();

    public List<ErrorAnalisis> getErrores() {
        return this.errores;
    }

    public List<DatoRecopilado> getDatos() {
        return this.datos;
    }

    public Usuario getUsuario(){
        return this.user;
    }

    public Formulario getForm(){
        return this.form;
    }

    private void addComponent() {
        form.getComponentes().add(comp);
        comp = new Componente();
    }

    public void syntax_error(Symbol s) {
        String ss =  (String) s.value;
        StringBuilder descripcion = new StringBuilder("Se esperaba: ");
        expected_token_ids().forEach(x -> descripcion.append(symbl_name_from_id(x)).append(", "));
        System.out.println(ss);
    }

:}

terminal        INICIO_USER, INICIO_FORM, INICIO_DATOS;
terminal        COLON, COMMA, OPEN_BRACE, CLOSE_BRACE, OPEN_ROUND_BRACKET, CLOSE_ROUND_BRACKET;

terminal String      PARAM_USUARIO, PARAM_PASSWORD, PARAM_NOMBRE, PARAM_INSTITUCION, PARAM_FECHA_CREACION, PARAM_FECHA_MODIFICACION;
terminal String      PARAM_ID, PARAM_TIEMPO, PARAM_NOMBRE_FORM, PARAM_TEMA, PARAM_USUARIO_CREACION;

terminal String      PARAM_COMPONENTES, PARAM_NOMBRE_CAMPO, PARAM_FORMULARIO, PARAM_CLASE, PARAM_TEXTO_VISIBLE;
terminal String      PARAM_RESPUESTA, PARAM_IDC, PARAM_OPCIONES, PARAM_FILAS, PARAM_COLUMNAS;

terminal String      CLASS_CAMPO_TEXTO, CLASS_AREA_TEXTO, CLASS_CHECKBOX, CLASS_RADIO, CLASS_FICHERO, CLASS_COMBO;
terminal String      OPCIONES, OPCIONES2;

terminal String      ID, FECHA, ENTERO, VALUE, LITERAL;

non terminal        inicio, startUserStructure, startFormStructure, startDataStructure, startStructure, endStructure;
non terminal        paramsUser, paramUser;
non terminal        paramsForm, paramForm;
non terminal        paramsData, paramData;

non terminal        componentStruct, components, component, params, param;

non terminal String         clase, alineacion, required, opciones;
non terminal String         value, fecha, entero, literal, id, tema, keyword;

inicio ::= startUserStructure
         | startFormStructure
         | startDataStructure
         ;
       

startUserStructure ::= INICIO_USER startStructure paramsUser endStructure;
startFormStructure ::= INICIO_FORM startStructure paramsForm endStructure;
startDataStructure ::= INICIO_DATOS startStructure paramsData endStructure;

startStructure ::= OPEN_ROUND_BRACKET OPEN_BRACE;


/*********************Producciones para parametros de usuario*********************/
paramsUser ::= paramUser COMMA paramsUser
             | paramUser
             ;

paramUser ::= PARAM_USUARIO COLON value:v               {:user.setNombre(v.replace("\"",""));:}
            | PARAM_PASSWORD COLON value:v              {:user.setPassword(v.replace("\"",""));:}
            | PARAM_NOMBRE COLON value:v                {:user.setName(v.replace("\"",""));:}
            | PARAM_INSTITUCION COLON value:v           {:user.setInstitucion(v.replace("\"",""));:}
            | PARAM_FECHA_CREACION COLON fecha:f        {:user.setFechaCreacion(f.replace("\"",""));:}
            | PARAM_FECHA_MODIFICACION COLON fecha:f    {:user.setFechaModificacion(f.replace("\"",""));:}
            ;
/*****************Fin de producciones para parametros de usuario*****************/



/*********************Producciones para parametros de form*********************/
paramsForm ::= paramForm COMMA paramsForm
             | paramForm
             ;

paramForm ::= PARAM_NOMBRE COLON value:v               {:form.setNombre(v.replace("\"",""));:}
            | PARAM_USUARIO_CREACION COLON value:v          {:form.setUsuarioCreacion(v.replace("\"",""));:}
            | PARAM_ID COLON id:i                           {:form.setId(i.replace("\"",""));:}
            | PARAM_FECHA_CREACION COLON fecha:f            {:form.setFechaCreacion(f.replace("\"",""));:}
            | PARAM_TIEMPO COLON entero:e                   {:form.setTiempo(e);:}
            | PARAM_TEMA COLON value:v                      {:form.setTema(v.replace("\"",""));:}
            | componentStruct
            ;

componentStruct ::= PARAM_COMPONENTES COLON OPEN_ROUND_BRACKET CLOSE_ROUND_BRACKET
                  | PARAM_COMPONENTES COLON OPEN_ROUND_BRACKET components CLOSE_ROUND_BRACKET
                  ;

components ::= component COMMA components
             | component
             ;

component ::= OPEN_BRACE params CLOSE_BRACE             {:addComponent();:}
            ;

params ::= param COMMA params
         | param
         ;

param ::= PARAM_IDC COLON id:i                           {:comp.setId(i.replace("\"",""));:}
        | PARAM_NOMBRE_CAMPO COLON value:v              {:comp.setNombreCampo(v.replace("\"",""));:}
        | PARAM_FORMULARIO COLON id:i                   {:comp.setFormulario(i.replace("\"",""));:}
        | PARAM_CLASE COLON clase:c                     {:comp.setClase(c.replace("\"",""));:}
        | PARAM_TEXTO_VISIBLE COLON literal:l           {:comp.setTextoVisible(l.replace("\"",""));:}
        | PARAM_RESPUESTA COLON literal:l               {:comp.setTextoVisible(l.replace("\"",""));:}
        | PARAM_OPCIONES COLON opciones:o               {:comp.setOpciones(o.replace("\"",""));:}
        | PARAM_FILAS COLON entero:e                    {:comp.setNoFilas(e.replace("\"",""));:}
        | PARAM_COLUMNAS COLON entero:e                 {:comp.setNoColumnas(e.replace("\"",""));:}
        ;
/*****************Fin de producciones para parametros de form*****************/



/*********************Producciones para parametros de data*********************/
paramsData ::= paramData COMMA paramsData
             | paramData
             ;

paramData ::= value:n COLON literal:v                     {:datos.add(new DatoRecopilado(n.replace("\"",""),v.replace("\"","")));:}
            ;
/*****************Fin de producciones para parametros de data*****************/

value ::= VALUE:v           {:RESULT = v;:}
        | id:i              {:RESULT = i;:}
        | fecha:f           {:RESULT = f;:}
        | entero:e          {:RESULT = e;:}
        | keyword:k         {:RESULT = k;:}
        ;

id ::= ID:i                 {:RESULT = i;:}
     ;

fecha ::= FECHA:f           {:RESULT = f;:}
        ;

literal ::= LITERAL:l       {:RESULT = l;:}
          | opciones:v        {:RESULT = v;:}
          ;

entero ::= ENTERO:e         {:RESULT = e;:}
         ;


clase ::= CLASS_CAMPO_TEXTO:c     {:RESULT = c;:}
        | CLASS_AREA_TEXTO:c      {:RESULT = c;:}
        | CLASS_CHECKBOX:c        {:RESULT = c;:}
        | CLASS_RADIO:c           {:RESULT = c;:}
        | CLASS_FICHERO:c         {:RESULT = c;:}
        | CLASS_COMBO:c           {:RESULT = c;:}
        ;


opciones ::= OPCIONES:o           {:RESULT = o;:}
           | OPCIONES2:o          {:RESULT = o;:}
           | value:v              {:RESULT = v;:}
           ;

keyword ::= clase:c                     {:RESULT = c;:}
          | alineacion:a                {:RESULT = a;:}
          | required:r                  {:RESULT = r;:}
          | PARAM_USUARIO:k             {:RESULT = k;:}
          | PARAM_PASSWORD:k            {:RESULT = k;:}
          | PARAM_NOMBRE:k              {:RESULT = k;:}
          | PARAM_INSTITUCION:k         {:RESULT = k;:}
          | PARAM_FECHA_CREACION:k      {:RESULT = k;:}
          | PARAM_FECHA_MODIFICACION:k  {:RESULT = k;:}
          | PARAM_ID:k                  {:RESULT = k;:}
          | PARAM_IDC:k                 {:RESULT = k;:}
          | PARAM_RESPUESTA:k           {:RESULT = k;:}
          | PARAM_TEMA:k                {:RESULT = k;:}
          | PARAM_USUARIO_CREACION:k    {:RESULT = k;:}
          | PARAM_COMPONENTES:k         {:RESULT = k;:}
          | PARAM_NOMBRE_CAMPO:k        {:RESULT = k;:}
          | PARAM_FORMULARIO:k          {:RESULT = k;:}
          | PARAM_CLASE:k               {:RESULT = k;:}
          | PARAM_TEXTO_VISIBLE:k       {:RESULT = k;:}
          | PARAM_OPCIONES:k            {:RESULT = k;:}
          | PARAM_FILAS:k               {:RESULT = k;:}
          | PARAM_COLUMNAS:k            {:RESULT = k;:}
          ;

endStructure ::= CLOSE_BRACE CLOSE_ROUND_BRACKET;